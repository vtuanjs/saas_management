load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load", "oci_push")
load("@tar.bzl", "mutate", "tar")

go_library(
    name = "cmd_lib",
    srcs = ["main.go"],
    importpath = "github.com/vtuanjs/saas_management/apps/saas_mngt_service/cmd",
    visibility = ["//visibility:private"],
    deps = [
        "//wire",
        "@org_golang_x_net//http2",
        "@org_golang_x_net//http2/h2c",
    ],
)

go_binary(
    name = "cmd",
    embed = [":cmd_lib"],
    visibility = ["//visibility:public"],
)

# Put app go_binary into a tar layer.
tar(
    name = "cmd_layer",
    srcs = [":cmd"],
    out = "cmd_layer.tar",
    mutate = mutate(strip_prefix = package_name() + "/cmd_"),
)

# This is the target that should be released to the target platform
platform_transition_filegroup(
    name = "transitioned_image",
    srcs = [":image"],
    target_platform = select({
        "@platforms//cpu:arm64": "@io_bazel_rules_go//go/toolchain:linux_arm64",
        "@platforms//cpu:x86_64": "@io_bazel_rules_go//go/toolchain:linux_amd64",
    }),
)

oci_image(
    name = "image",
    base = "@distroless_base",
    entrypoint = ["/cmd"],
    labels = {
        "org.opencontainers.image.source": "https://github.com/vtuanjs/saas_management",
        "build.version": "latest",
    },
    tars = [":cmd_layer"],
)

oci_load(
    name = "load",
    image = ":transitioned_image",
    repo_tags = ["saas_mngt_service:latest"],
)

oci_push(
    name = "push",
    image = ":image",
    # Can set via flag: -- --repository $DOCKER_REPOSITORY
    repository = "{{DOCKER_REPOSITORY}}",
)
